CREATE PROCEDURE DAXVEHICLES 
    @SHIPCPYCONTAINERID NVARCHAR(30),
    @parkingSpaceTransTypeActual INT,
	@external_movement INT = 0
AS
BEGIN
    DECLARE @ResultTable TABLE (
        SHIPPINGCPYVENDTABLEVEHICLESID NVARCHAR(40),
        INTRASTATTRANSPORT NVARCHAR(10)
    );

    DECLARE @SHIPPINGCPYVENDTABLERECID BIGINT;
	DECLARE @RECID BIGINT;
    DECLARE @PARKINGSPACETRANSTYPE INT;
    DECLARE @SHIPPINGCPYTYPE INT;

    -- Obtener los valores necesarios de SHIPCPYCONTAINERTABLE
    SELECT @SHIPPINGCPYVENDTABLERECID = SHIPPINGCPYVENDTABLERECID,
           @SHIPCPYCONTAINERID = SHIPCPYCONTAINERID,
		   @RECID = RECID
    FROM SHIPCPYCONTAINERTABLE
    WHERE SHIPCPYCONTAINERID = @SHIPCPYCONTAINERID;

    -- Determinar el RECID de SHIPPINGCPYVENDTABLE basado en shipCpyContainerState
    IF @parkingSpaceTransTypeActual IN (2, 9) -- Si la posicion es, [Reservado -> Ingresado] [Traslado preparado entrada externa -> Ingresado]
    BEGIN
        SELECT TOP 1 @PARKINGSPACETRANSTYPE = T1.PARKINGSPACETRANSTYPE
        FROM SHIPCPYCONTAINERTRANS T1
        WHERE T1.SHIPCPYCONTAINERTABLERECID = @RECID
        ORDER BY T1.CREATEDDATETIME DESC, T1.RECID DESC;

        IF @PARKINGSPACETRANSTYPE = 13 -- Si es, Traslado de salida externo
        BEGIN
            SELECT @SHIPPINGCPYTYPE = SHIPPINGCPYTYPE
            FROM SHIPPINGCPYVENDTABLE
            WHERE RECID = @SHIPPINGCPYVENDTABLERECID;

            IF @SHIPPINGCPYTYPE = 0 -- Si la naviera es comercial
            BEGIN
                SELECT TOP 1 @SHIPPINGCPYVENDTABLERECID = RECID
                FROM SHIPPINGCPYVENDTABLE
                WHERE SHIPPINGCPYTYPE = 1;
            END
        END
    END
    ELSE IF @parkingSpaceTransTypeActual IN (4, 7, 10) -- Si la posicion es, [abierto -> Cerrado] [abierto -> Traslado Preparado externo] [Cerrado -> Salida Final] [Traslado preparado salida externa -> Salida Externa]
    BEGIN
        SELECT @SHIPPINGCPYTYPE = SHIPPINGCPYTYPE
        FROM SHIPPINGCPYVENDTABLE
        WHERE RECID = @SHIPPINGCPYVENDTABLERECID;

        IF ((@parkingSpaceTransTypeActual = 10) OR (@parkingSpaceTransTypeActual = 4 AND @external_movement = 1)) AND @SHIPPINGCPYTYPE = 0 -- Si es Salida Externa รณ es traslado preparado externo y la naviera es comercial
        BEGIN
            SELECT TOP 1 @SHIPPINGCPYVENDTABLERECID = RECID
            FROM SHIPPINGCPYVENDTABLE
            WHERE SHIPPINGCPYTYPE = 1;
        END
    END

    -- Obtener resultados finales
    INSERT INTO @ResultTable (SHIPPINGCPYVENDTABLEVEHICLESID, INTRASTATTRANSPORT)
    SELECT T1.SHIPPINGCPYVENDTABLEVEHICLESID, T2.INTRASTATTRANSPORT
    FROM SHIPPINGCPYVENDTABLEVEHICLES T1
    INNER JOIN SHIPPINGCPYVENDTABLE T2 ON T1.SHIPPINGCPYVENDTABLERECID = T2.RECID
    WHERE T1.SHIPCPYINACTIVE = 0
    AND T2.RECID = @SHIPPINGCPYVENDTABLERECID;

    -- Devolver los resultados
    SELECT * FROM @ResultTable;
END;